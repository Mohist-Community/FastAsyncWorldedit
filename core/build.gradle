repositories {
    flatDir {dirs 'lib'}
    maven {url "http://ci.athion.net/job/PlotSquared-Legacy/ws/mvn/"}
}
dependencies {
    testCompile 'junit:junit:4.12'
    compile 'org.yaml:snakeyaml:1.16'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'net.fabiozumbi12:redprotect:1.9.6'
    compile group: "com.plotsquared", name: "plotsquared-api", version: "latest"
    compile 'org.primesoft:BlocksHub:2.0'
    compile 'com.github.luben:zstd-jni:1.1.1'
    compile 'co.aikar:fastutil-lite:1.0'
    compile(group: 'com.sk89q.worldedit', name: 'worldedit-core', version:'6.1.3-SNAPSHOT') {
        exclude(module: 'bukkit-classloader-check')
    }
}

processResources {
    from('src/main/resources') {
        include 'fawe.properties'
        expand(
                version: "${project.parent.version}",
                name: project.parent.name,
        )
    }
}

jar.archiveName="fawe-api-${project.parent.version}.jar"
jar.destinationDir = file '../mvn/com/boydti/fawe-api/' + project.parent.version

task copyFiles {
    doLast {
        copy {
            from "../mvn/com/boydti/fawe-api/${project.parent.version}/"
            into '../mvn/com/boydti/fawe-api/latest/'
            include('*.jar')
            rename ("fawe-api-${project.parent.version}.jar", 'fawe-api-latest.jar')
        }
    }
}

build.finalizedBy(copyFiles)